apply plugin: 'com.android.library'
def isReleaseBuildType = false
gradle.startParameter.taskNames.each({
    String taskNameL = it.toLowerCase()
    if (taskNameL.contains("release")) {
        isReleaseBuildType = true
    }
    if (taskNameL.equalsIgnoreCase('uploadArchives')) {
        isReleaseBuildType = true
    }
})

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        renderscriptTargetApi rootProject.ext.renderscriptTargetApi
        renderscriptSupportModeEnabled rootProject.ext.renderscriptSupportModeEnabled
        consumerProguardFiles 'consumer-rules.pro'
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_PLATFORM=android-13', '-DANDROID_TOOLCHAIN=clang'
                cppFlags "-std=c++11 -frtti -fexceptions"
            }
        }
        externalNativeBuild.cmake.abiFilters = isReleaseBuildType ? ['armeabi-v7a', 'arm64-v8a'] : ['armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64']
    }

    lintOptions {
        abortOnError false
    }

    externalNativeBuild {
        cmake {
            path 'src/main/jni/CMakeLists.txt'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.annotation:annotation:1.1.0'
}

ext {
    bintrayRepo = 'Hoko'
    bintrayName = 'HokoBlurDrawable'

    publishedGroupId = 'com.hoko'
    libraryName = 'HokoBlurDrawable'
    artifact = 'hoko-blur-drawable'

    libraryDescription = 'HokoBlurDrawable is an Android component which provides dynamic blur effect.'

    siteUrl = 'https://github.com/HokoFly/HokoBlurDrawable'
    gitUrl = 'https://github.com/HokoFly/HokoBlurDrawable.git'

    libraryVersion = HOKO_BLUR_DRAWABLE_VERSION

    developerId = 'HokoFly'
    developerName = 'HokoFly'
    developerEmail = 'yuxfzju@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

apply from: rootProject.file('gradle/bintray.gradle')